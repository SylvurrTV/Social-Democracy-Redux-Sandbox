title: Military Policy
new-page: true
is-card: true
tags: govt_affairs
on-arrival: military_policy_timer += 6; month_actions += 1
view-if: spd_in_government = 1 and reichswehr_minister_party = "SPD" and military_policy_timer <= 0
card-image: img/portraits/LeberJulius.jpg
#subtitle: Dealing with the Reichswehr.

= Military Policy

The Reichswehr is a deeply reactionary institution that has been generally hostile to any notion of socialism or democracy. However, we now control the military as part of the government. What should we do to reform it?

- @increase_funding: Increase funding for the military to purchase its loyalty.
- @decrease_funding: Reduce funding for the military so we have more for social spending.
- @reform: Conduct a thorough reform of the ranks, reducing the influence of reactionaries and making it more hospitable to us.
- @root: No change to military policy.
- @easy_discard
- @cancel_advisor_action

@increase_funding
subtitle: -1 budget
choose-if: budget >= 1
unavailable-subtitle: We do not have the budget.
on-arrival: rearmament_vote = 1
go-to: rearmament_done if not legislature_enabled; legislative_menu if legislature_enabled

We propose a bill to increase military spending.

@rearmament_done
on-arrival: budget -= 1; coalition_dissent -= 1  if coalition_dissent > 0; reichswehr_loyalty += 0.07 if reichswehr_loyalty < 1; reichswehr_militancy += 1; left_dissent += 5; kpd_coalition_dissent += 1; west_relation -= 1; east_relation -= 1; reparations_negotiation -= 1; eu_progress -= 1; economic_growth += 0.1

The military and the center-right parties are pleasantly surprised that we would increase military funding. Our membership tends to be anti-military, and is less enthused. Germany's neighbors are watching our military buildup with worry. [? if reparations >= 0 : This is not good for the reparations negotiations. ?]

@decrease_funding
subtitle: +1 budget
unavailable-subtitle: There is nothing left to cut.
choose-if: reichswehr_strength >= 20 or reichswehr_militancy >= 1
on-arrival: disarmament_vote = 1
go-to: disarmament_done if not legislature_enabled; legislative_menu if legislature_enabled

We propose a bill to increase military spending.

@disarmament_done
on-arrival: demilitarizations += 1; budget += 1; coalition_dissent += 1 if in_grand_coalition == 1; dvp_relation -= 8; reichswehr_loyalty -= 0.05 if reichswehr_loyalty > 0; coup_progress += 1; coup_progress += 0.5 if reichswehr_loyalty < 0.2; coup_progress += 0.5 if reichswehr_loyalty < 0.1; coup_progress += 0.5 if reichswehr_loyalty <= 0; coup_progress += 0.5 if harzburg_front_seen; reichswehr_goal_completed = 1; coalition_dissent += 1 if coup_progress >= 4; coalition_dissent += 1 if coup_progress >= 6; coalition_dissent += 1 if coup_progress >= 8; economic_growth -= 0.1

The center-right parties are committed to the military and deeply opposed to our budget cuts. But the decision has been made, and now we must choose how best to implement it, as there are two main ways in which we support the German army: paying and supplying its soldiers on the one hand, but also arming the soldiers with arms and ammunition. In cutting it, we can lay off soldiers and downsize the army, or we can demilitarize it, reducing the quality and quantity of weapons.

Demilitarization is preferred by our coalition partners and will be harder to rally against by the right, as it is a less blatant move. It will weaken the army more, however. 

- @demilitarize: We will demilitarize the army.
- @downsize: We will downsize the army.
#- @disband: We will disband the army.

@demilitarize
choose-if: reichswehr_militancy >= 1
unavailable-subtitle: The army is but a glorified police force now.
on-arrival: reichswehr_militancy -= 0.5; coalition_dissent -= 1 if coup_progress < 4; dvp_relation += 2; sh_militancy += 0.05; rural_nsdap += 2; rural_dnvp += 1; old_middle_nsdap += 1; new_middle_nsdap += 1

The army has fewer guns, and the guns it does have are lower quality. The reaction from the right is muted by the less noticeable nature of our cuts, but the army remains angry that we are attacking their position, and weapons have gone missing from our stockpiles.

[? if coup_progress < 5 : There is no hint of a coup yet, but we do not know where the line will be drawn. ?][? if coup_progress >= 5 and coup_progress < 8 : Rumors of a coup have begun to spread around, though in terms of actual evidence, there is nothing yet. We must proceed with caution. ?][? if coup_progress >= 8 : Arms and ammunition seem to disappear mysteriously from military supplies, while reactionaries increasingly join the army. Officers meet through the nights, often with Harzburg Front officials as well, and the army's already huge degree of independence seems to somehow grow. Most damning of all is the Harzburg Front's recent condemnation of demilitarization, which seems almost a veiled threat aimed at the government. There can be no uncertainty: a coup plot is in motion, and we might not have much time to prevent it. ?]

@downsize
view-if: reichswehr_strength >= 20
on-arrival: reichswehr_strength -= 20; sh_strength += 15; sa_strength += 5; unemployed += 0.1; unemployed_dnvp += 1; unemployed_nsdap += 2; new_middle_nsdap += 2; old_middle_nsdap += 2; rural_nsdap += 3; rural_dnvp += 1; rural_nsdap -= 1 if (not black_thursday_seen or return_to_normalcy); workers_spd -= 2 if nationalism >= 50; nationalism += 2

The army has fewer soldiers, as we decrease its size even further below the already small limit set out for us by Versailles. Many demobilized soldiers have joined the right-wing paramilitaries such as the Stahlhelm and SA, while the right rallies for the defense of Germany.

Our coalition is upset, as is the army that we are directly attacking their powerful position. [? if coup_progress < 5 : There is no hint of a coup yet, but we do not know where the line will be drawn. ?][? if coup_progress >= 5 and coup_progress < 8 : Rumors of a coup have begun to spread around, though in terms of actual evidence, there is nothing yet. We must proceed with caution. ?][? if coup_progress >= 8 : Arms and ammunition seem to disappear mysteriously from military supplies, while reactionaries increasingly join the army. Officers meet through the nights, often with Harzburg Front officials as well, and the army's already huge degree of independence seems to somehow grow. Most damning of all is the Harzburg Front's recent condemnation of demilitarization, which seems almost a veiled threat aimed at the government. There can be no uncertainty: a coup plot is in motion, and we might not have much time to prevent it. ?]

@disband
view-if: reichswehr_strength < 20 and reichswehr_militancy < 1

WIP

@reform
subtitle: -1 budget
choose-if: budget >= 1 and reichswehr_loyalty < 1
unavailable-subtitle: [? if budget < 1 : We do not have the budget. ?][? if reichswehr_loyalty >= 1 : The Reichswehr is already fully loyal to the democratic government. ?]
on-arrival: military_reform_vote = 1
go-to: military_reform_done if not legislature_enabled; legislative_menu if legislature_enabled

We propose a bill to perform substantial reforms in the army.

@military_reform_done
on-arrival: coalition_dissent += 1 if in_grand_coalition == 1; coalition_dissent += 1 if coup_progress >= 4; coalition_dissent += 1 if coup_progress >= 6; coalition_dissent += 1 if coup_progress >= 8; coup_progress += 0.5 if reichswehr_loyalty < 0.25; coup_progress += 1 if reichswehr_loyalty < 0.1; coup_progress += 1 if reichswehr_loyalty = 0; coup_progress -= 1 if reichswehr_loyalty >= 0.4 and coup_progress > 0; coup_progress -= 1 if reichswehr_loyalty >= 0.6 and coup_progress > 0; reichswehr_loyalty += 0.15; reichswehr_militancy -= 0.25 if reichswehr_loyalty < 0.3; reichswehr_militancy -= 0.1 if reichswehr_loyalty < 0.5 and reichswehr_loyalty >= 0.3; reichswehr_militancy -= (coup_progress/100); sh_militancy += 0.05 if reichswehr_loyalty < 0.3; sh_militancy += 0.05 if reichswehr_militancy < 0.5; sh_militancy += (coup_progress/200); budget -= 1; dvp_relation -= 5; pacifism += 1; democratization += 1;

Our efforts are at least somewhat successful; we have changed the recruiting and training patterns so that more soldiers from non-reactionary backgrounds can join, hopefully changing the culture of the forces. We have also removed some of the officers who supported the previous Kapp putsch. [? if reichswehr_militancy >= 3 : A downside of removing reactionary officers and instituting new republican ones is that the republican replacements tend to be less experienced and require more training. This leaves the Reichswehr weakened by our reforms, for now, though perhaps that is a good thing. ?]

# todo: option to increase training (-1 budget) to make up the loss

[? if coup_progress > 6 and reichswehr_loyalty < 0.6 : The reactionaries, seeing our attempts to weaken their influence have only accelerated their existing coup plans, spurred into action by our moves against them. ?] The center-right parties are committed to the military and deeply suspicious of our efforts to reform it.
