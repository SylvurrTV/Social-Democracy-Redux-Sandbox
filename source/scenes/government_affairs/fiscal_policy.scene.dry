title: Fiscal Policy
new-page: true
is-card: true
tags: govt_affairs
on-arrival: fiscal_policy_timer += 6; month_actions += 1
view-if: spd_in_government = 1 and finance_minister_party = "SPD" and fiscal_policy_timer = 0
card-image: img/portraits/HilferdingRudolf.jpg
go-to: fiscal_menu
#subtitle: Issues of taxation, budget, and tariffs.

= Fiscal Policy

Fiscal policy involves issues of taxation and budget.

# TODO: tax update :sob:

@fiscal_menu

Upper-income tax rates: [+ upper_tax_rates : taxation +]

Lower-income tax rates: [+ lower_tax_rates : taxation +]

- @raise_progressive: Make the tax system more progressive by raising taxes on the rich and cutting them for the poor.
- @raise_rich: Increase taxes on the rich.
- @raise_overall: Increase overall taxes.
- @cut_overall: Cut overall taxes.
- @cut_overall_wtb: Cut overall taxes.
- @regressive
- @tariffs
- @root: Do not change the tax system.
- @easy_discard
- @cancel_advisor_action

@raise_progressive
subtitle: This does not change overall revenue. [? if lower_tax_rates < -5 : Lower-class tax rates are too low to cut anymore. ?] [? if upper_tax_rates > 5 : Upper-class tax rates are too high to increase. ?][? if status_quo : We have agreed to leave the budget untouched. ?]
choose-if: lower_tax_rates >= -5 and upper_tax_rates <= 5 and not status_quo
on-arrival: progressive_tax_vote = 1
go-to: progressive_tax_done if not legislature_enabled; legislative_menu if legislature_enabled

We propose a bill to make taxation more progressive.

@progressive_tax_done
on-arrival: workers_spd += 6*(1-dissent); old_middle_spd -= 3; new_middle_spd -= 3; new_middle_spd += 1 if new_middle_conscious; coalition_dissent += 1 if in_grand_coalition; dvp_relation -= 4; finance_goal_completed += 1; upper_tax_rates += 1; lower_tax_rates -= 1; capital_strike_progress += 1; capital_strike_progress += 1 if upper_tax_rates > 1; capital_strike_progress += 1 if upper_tax_rates > 3; socialism += 2

Making the tax system more progressive is popular among our base, but it upsets our bourgeois coalition partners, as well as our minimal number of bourgeois voters.

@raise_rich
subtitle: +2 budget
unavailable-subtitle: [? if not status_quo : Tax rates are already too high. ?][? if status_quo : We have agreed to leave the budget untouched. ?]
choose-if: upper_tax_rates <= 5 and not status_quo
on-arrival: raise_rich_vote = 1
go-to: raise_rich_done if not legislature_enabled; legislative_menu if legislature_enabled

We propose a bill to raise taxes on higher income earners.

@raise_rich_done
on-arrival: upper_tax_rates += 1; capital_strike_progress += 1; capital_strike_progress += 1 if upper_tax_rates > 1; capital_strike_progress += 1 if upper_tax_rates > 3; workers_spd += 4*(1-dissent); old_middle_spd -= 4; new_middle_spd -= 4; new_middle_spd += 1 if new_middle_conscious; coalition_dissent += 1 if in_grand_coalition; coalition_dissent += 1 if capital_strike_progress >= 5; budget += 2; dvp_relation -= 4; unemployed += 1; finance_goal_completed += 1; inflation -= 0.6; economic_growth -= 0.4; socialism += 1

Raising taxes on the rich is popular among our base, but it alienates the bourgeoisie. Increasing taxes also decreases economic demand, which may lead to more unemployment.

@cut_overall
view-if: wtb_adopted == 0 or budget >= 2
subtitle: -2 budget
unavailable-subtitle: -2 budget - [? if upper_tax_rates < -5 or lower_tax_rates < -5 : Tax rates are already too low. ?][? if status_quo : We have agreed to leave the budget untouched. ?]
choose-if: budget >= 2 and upper_tax_rates >= -5 and lower_tax_rates >= -5 and not status_quo
on-arrival: cut_vote = 1
go-to: cut_done if not legislature_enabled; legislative_menu if legislature_enabled

We propose a bill to lower taxes across the board.

@cut_done
on-arrival: budget -= 2; upper_tax_rates -= 1; lower_tax_rates -= 1; new_middle_spd += 5*(1-dissent); old_middle_spd += 4*(1-dissent); workers_spd += 3*(1-dissent); coalition_dissent += 1 if budget <= 2; dvp_relation += 5*(1-dissent) if budget > 2; z_relation += 5*(1-dissent) if budget > 2; unemployed -= 1 if unemployed >= 5; capital_strike_progress -= 1 if upper_tax_rates < 0; inflation += 0.4; economic_growth += 0.4; socialism -= 1

Cutting taxes benefits most the rich and middle-class, who pay more taxes than the working class. Our coalition partners are happy at our orthodox economic course.

@cut_overall_wtb
view-if: wtb_adopted == 1 and budget < 2
choose-if: upper_tax_rates >= -5 and lower_tax_rates >= -5 and not status_quo
subtitle: -2 budget. We will enter deficit spending. [? if upper_tax_rates < -5 or lower_tax_rates < -5 : Tax rates are already too low. ?][? if status_quo : We have agreed to leave the budget untouched. ?]
on-arrival: cut_vote = 1
go-to: cut_done_wtb if not legislature_enabled; legislative_menu if legislature_enabled

We propose a bill to lower taxes across the board.

@cut_done_wtb
on-arrival: budget -= 2; upper_tax_rates -= 1; lower_tax_rates -= 1; new_middle_spd += 5*(1-dissent); old_middle_spd += 4*(1-dissent); workers_spd += 3*(1-dissent); dvp_relation -= 3; z_relation -= 2; unemployed -= 1 if unemployed >= 5; unemployed -= 1 if works_program > 1; capital_strike_progress -= 1 if upper_tax_rates < 0; inflation += 0.7; hyperinflation += 0.1; coalition_dissent += 1 if in_grand_coalition; center_dissent += 5; economic_growth_rate += 0.6; socialism -= 2

Cutting taxes benefits most the rich and middle-class, who pay more taxes than the working class. Our coalition partners are upset at our deficit spending, warning that it will lead to a repeat of the hyperinflation in the early history of our republic. The Center Marxists join in these warnings.

@raise_overall
subtitle: +3 budget
unavailable-subtitle: [? if not status_quo or welfare < 1 : Tax rates are already too high. ?][? if status_quo or welfare >= 1 : We have agreed to leave the budget untouched. ?]
choose-if: upper_tax_rates <= 5 and lower_tax_rates <= 5 and (not status_quo or welfare < 1)
on-arrival: raise_vote = 1
go-to: raise_done if not legislature_enabled; legislative_menu if legislature_enabled

We propose a bill to lower taxes across the board.

@raise_done
on-arrival: budget += 3; new_middle_spd -= 4; old_middle_spd -= 4; workers_spd -= 3; rural_spd -= 3; unemployed += 2; kpd_coalition_dissent += 1; coalition_dissent += 1 if welfare > 1; coalition_dissent += 1 if budget >= 6; lower_tax_rates += 1; upper_tax_rates += 1; capital_strike_progress += 1 if upper_tax_rates > 1; capital_strike_progress += 1 if upper_tax_rates > 2; workers_spd -= 5 if lower_tax_rates > 1; inflation -= 1.1; economic_growth -= 0.6;

Raising taxes hurts our support among all those who pay taxes. [? if in_grand_coalition or in_weimar_coalition : However, our coalition partners are more supportive of the idea of "shared sacrifice" than simply taxing the rich. ?] Increasing taxes also decreases economic demand, which may lead to more unemployment.

@regressive
title: Lower taxes on the rich, while increasing consumption taxes (which affect the working class more).
subtitle: This does not change overall revenue. [? if upper_tax_rates < -5 : Rich tax rates are too low to cut anymore. ?] [? if lower_tax_rates > 5 : Lower-class tax rates are too high to increase. ?]
choose-if: lower_tax_rates <= 5 and upper_tax_rates >= -5
on-arrival: regressive_vote = 1
go-to: regressive_done if not legislature_enabled; legislative_menu if legislature_enabled

We propose a bill to make the tax system more regressive.

@regressive_done
on-arrival: workers_spd -= 6; new_middle_spd += 5*(1-dissent); old_middle_spd += 4*(1-dissent); dvp_relation += 5; z_relation += 4; coalition_dissent -= 1 if in_grand_coalition and coalition_dissent > 0; kpd_coalition_dissent += 1; upper_tax_rates -= 1; lower_tax_rates += 1; workers_spd -= 5 if lower_tax_rates >= 2; capital_strike_progress -= 1 if upper_tax_rates < 0; socialism -= 3

Our regressive taxation policy benefits the rich while hurting the poor and working-class. Our bourgeois coalition partners approve of this orthodox economic course.

@tariffs
title: Change tariffs instead.

Tariffs, or taxes on international imports, are another way to finance our government.

Historically, socialists have been in favor of free trade and a low-tariff regime. Workers in export-oriented industries benefit from free trade, and they also benefit from lower prices for imported necessities. Do the present economic circumstances change these calculations?

Current tariff level: [+ tariffs : taxation +]

- @raise_tariffs
- @lower_tariffs
- @fiscal_menu: Change domestic taxes instead of tariffs.
- @root: Do not change tariffs or taxes.
- @easy_discard

@raise_tariffs
title: Raise tariffs on imports.
subtitle: +2 budget. Other countries will retaliate by increasing tariffs of their own. [? if tariffs > 5 : Further increases in tariffs will not raise revenue, as we have already nearly reached econonmic autarky. ?]
choose-if: tariffs <= 5
on-arrival: tariff_hike_vote = 1
go-to: tariff_hike_done if not legislature_enabled; legislative_menu if legislature_enabled

We propose a bill to increase tariff rates.

@tariff_hike_done
on-arrival: tariffs += 1; inflation += 1 if inflation > 0; inflation += 0.5 if inflation <= 0; unemployed += 1.5; budget += 2; workers_spd -= 5; unemployed_spd -= 5; rural_spd += 4*(1-dissent); old_middle_spd += 3*(1-dissent); ddp_relation -= 5; dvp_relation -= 2; west_relation -= 1; east_relation -= 1; reparations_negotiation -= 2; eu_progress -= 1 if eu_progress > 0; peoples_party_support += 1; economic_growth -= 0.4; capital_strike_progress += 1 if tariffs >= 3; socialism -= 2

Increasing tariffs leads to higher prices for basic goods, increasing inflation and hurting consumption among the working class. Retaliatory tariffs reduce the competitiveness of German exports, raising unemployment among workers. They also lead to an atmosphere of international suspicion, reducing our ability to negotiate with other countries.

On the other hand, agricultural producers and some small businesses benefit from higher tariffs, as farmers can now command higher prices for grain, and formerly uncompetitive businesses are now able to function.

@lower_tariffs
title: Lower tariffs on imports.
choose-if: budget >= 2 and tariffs > -5
subtitle: -2 budget. [? if tariffs <= -5 : There are no tariffs left to cut. ?]
on-arrival: tariff_cut_vote = 1
go-to: tariff_cut_done if not legislature_enabled; legislative_menu if legislature_enabled

We propose a bill to decrease tariff rates.

@tariff_cut_done
on-arrival: tariffs -= 1; budget -= 2; inflation -= 0.5; workers_spd += 4*(1-dissent); unemployed_spd += 4*(1-dissent); rural_spd -= 5; old_middle_spd -= 3; west_relation += 1; east_relation += 1; eu_progress += 1 if eu_progress > 0; economic_growth += 0.4; socialism += 2

Lowering tariffs helps workers and the urban poor by reducing prices for basic necessities. In addition, other countries are pleased by lower tariffs, and are more willing to negotiate on many issues.

Farmers are hurt by this policy, as cheap imported grain leads to ruin for some small producers. In general, cheap imports are detrimental for small-scale industries aimed at the domestic market.