title: Hitler appointed Chancellor!
subtitle: The beginning of the end...
view-if: year >= 1933 and president = "Hindenburg" and (chancellor = "Papen" or chancellor = "Schleicher") and in_grand_coalition = 0 and spd_in_government = 0 and largest_party = "NSDAP" and hitler_deported = 0 and not presidential_powers and not hitler_seen and not schleichers_scheme_success
on-departure: chancellor = "Hitler"; chancellor_party = "NSDAP"; hitler_seen = 1; hitler_month = month; hitler_year = year; spd_disarray = 0
tags: event
new-page: true
go-to: choose if not spd_prussia and not z_prussia; prussian_overthrow if spd_prussia or z_prussia

= Hitler Appointed Chancellor!

Internal scheming by the camarilla surrounding the president has caused President Hindenburg to appoint Hitler as chancellor, replacing the unpopular [+ chancellor +]. [? if hitler_deported : Despite his earlier deportation, the Nazi leaders and their reactionary allies have connived for the president to give him German citizenship after all. ?] Some of the reactionaries surrounding Hindenburg believe that he will restore Germany to its supposed former glory, while others believe that they can control him and his movement.

He has promised a campaign of revenge against the socialists, and the Nazis have already spilled blood. In his published manifesto, he promises a campaign of unspeakable horror against Jews, and endless wars of violent expansion that will ruin all of Europe.

[? if spd_prussia or z_prussia : As his first move towards consolidating total power, Hitler has ordered the dismissal of the Prussian state government in a procedyre known as the *Reichsexekution*. Many of our members are ready to resist, as caving would surely mean the end. ?]

What are we to do now?

@choose

- @fight: We will fight on the streets to prevent Hitler from taking power.
- @give_up_now: We will vote against the nazis in the Reichstag.


@fight
on-arrival: spd_rebel = 1

Do we realize that this means civil war, against the full force of the state?

- @civil_war: Yes.
- @give_up_now: We are not ready for war. Instead, we will vote against the nazis in the Reichstag.

@give_up_now
on-arrival: {!
Q.next_election_time = Q.time+2;
Q.next_election_month = Q.month+2; 
Q.next_election_year = Q.year; 
if (Q.month > 12) {
    Q.next_election_year += 1;
    Q.next_election_month -= 12;
} 
Q.impending_fire = 1; 
Q.rb_militancy -= 0.1;
Q.rb_strength -= 250; 
Q.sa_strength += 100; 
Q.sa_militancy += 0.05; 
Q.workers_nsdap += 6; 
Q.new_middle_nsdap += 8; 
Q.old_middle_nsdap += 8; 
Q.rural_nsdap += 10; 
Q.unemployed_nsdap += 12;
Q.workers_kpd -= 6;
Q.workers_spd -= 3;
Q.kpd_relation += 10; 
Q.unemployed_kpd -= 8;
Q.unemployed_spd -= 4;
!}

Speeches are made. Votes are held. Bullets fly in the streets. Some socialists are killed or jailed. Others flee the country. What can votes even do at this juncture?

Snap elections are called, for [+ next_election_month : month +] [+ next_election_year +]. The SA and Stalhelm begin a campaign of violent intimidation in the streets, determined to win the next elections by whatever means it takes. 

@prussian_overthrow

- @defend
- @surrender


@defend
title: Fight against the overthrow attempt!
subtitle: Labor will call a general strike. The Reichsbanner and the loyal elements of the Prussian police will resist the coup.
unavailable-subtitle: We do not have the forces to resist.
choose-if: rb_strength*rb_militancy >= 200

The Reichswehr and Stahlhelm are already surrounding Berlin, and the SA is supporting them. This could be civil war. We do realize that, correct? 

- @defend_2: We will fight, even if it leads to a civil war.
- @surrender

@defend_2
on-arrival: resist_coup = 1; defense_strength = rb_strength*rb_militancy*(1-dissent/2); defense_strength += prussian_police_strength*prussian_police_militancy*prussian_police_loyalty if spd_prussia; enemy_strength = sa_strength*sa_militancy + sh_strength*sh_militancy; enemy_strength += reichswehr_strength*reichswehr_militancy*(1-reichswehr_loyalty)/2; defense_strength += workers_spd_normalized*(2 - dissent)*(2 - unemployed/100);
go-to: defend_2_civil_war if defense_strength < enemy_strength; defend_2_victory if defense_strength >= enemy_strength

@defend_2_victory
on-arrival: sa_strength *= 0.8; sh_strength *= 0.8; pro_republic += 10; workers_spd += 6; unemployed_spd += 6; workers_nsdap -= 6; unemployed_nsdap -= 6; coup_victory = 1; resisted_hitler = 1; hitler_been_sacked = 0
achievement: bollwerk_der_demokratie
go-to: defend_2_victory_2

Our forces confront the government troops and their reactionary auxiliaries, and somehow, our enemies retreat without a fight! They were overwhelmed by our display of strength, and have chosen to slink away. Chancellor [+ chancellor +] withdraws the dismissal of the Prussian government, at least for now.

The show of force has reinvigorated support for the republic and for the SPD, and has weakened the fascists.

@defend_2_victory_2
achievement: drei_pfeile


@defend_2_civil_war
on-arrival: spd_rebel = 1

Our forces confront the government troops and their supporters on the streets of Berlin, and armed fighting erupts. The reactionaries do not budge from their positions. We are caught at a disadvantage.

- @civil_war: It is a civil war.




@surrender
title: Give up on the Prussian government.
on-arrival: spd_prussia = 0; workers_spd -= 5; left_dissent += 10; labor_dissent += 10; rb_militancy = rb_militancy/1.25; rb_strength -= 200;
go-to: give_up_now

A pall of hopelessness and despair descends over social democracy. We have failed. Many in the Reichsbanner simply quit, demoralized by their leaders' lack of a call to action.
