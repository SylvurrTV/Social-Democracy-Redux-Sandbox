title: Political Crisis
subtitle: A political crisis wreaks havoc in Germany...
view-if: political_crisis_progress >= 3
new-page: true
tags: event
max-visits: 1
on-arrival: coup_progress += no_majority_elections/2; {!
Q.strife = Math.round(Q.strife);
!}
go-to: marx_crisis if president == "Marx"; hindenburg_crisis if president == "Hindenburg" and not presidential_powers; gessler_crisis if president == "Gessler" and not presidential_powers; adenauer_crisis if president == "Adenauer" and not presidential_powers; eckener_crisis if president == "Eckener" and not presidential_powers; nothing_to_do if presidential_powers

= Political Crisis

For multiple elections in a row now, no government has been able to be formed. This has been destroying the trust of the German people in their democracy, and all eyes turn to the president to see what he will do.

@marx_crisis
on-arrival: chancellor = "Groener"; spd_toleration = 0; chancellor_party = "I"; time_to_election = 12+strife; strife -= 3; sa_militancy -= 0.1; rb_militancy -= 0.1 if rb_militancy >= 0.1; rfb_militancy -= 0.1; sh_militancy -= 0.05; reichswehr_loyalty += 0.1;
go-to: fascist_uprising if coup_progress >= 10 and sa_force+sh_force >= 50 and nsdap_r+dnvp_r >= 40; next_election_time if coup_progress < 10 or sa_force+sh_force < 45 or nsdap_r+dnvp_r < 35

Wilhelm Marx has become increasingly influenced by an inner cadre of military men and seeing the total inability of the Reichstag to form a government has caved to their increasing calls for martial law. 

He has asked Wilhelm Groener to lead a temporary military government and, wielding the powers of Article 48 of the German constitution, declared martial law and cancelled the next elections. 

@hindenburg_crisis
on-arrival: chancellor = "Seldte"; chancellor_party = "DNVP"

Seeing the continuing collapse of order in Germany, and convinced that they would be able to quell any resistance, President [+ president +] and Chancellor Seldte have come together to declare a state of martial law in Germany. Though this is unconstitutional, the courts have little sway over the enthusiastic army.

- @seldte_takes_power: What now?

@seldte_takes_power
subtitle: The beginning of the end.
on-arrival: reichswehr_loyalty -= 0.1; stalhelm_militancy += 1.5

The destruction of German democracy has begun, not at the hands of the Nazis but at the hands of the reactionaries who wish to see a return to the days of the German Empire. If we do not fight back now, then we may never get another chance, and all we as a party have stood for will be gone in an instant.

- @fight_harzburg: Mobilize our forces and declare a general strike!
- @reactionary_victory: We have lost.

@reactionary_victory

Party offices are raided by army and police forces. Our leaders are rounded up and arrested. Any and all of our reforms are undone, and a police state under the army's control is cemented. The Nazis have failed to take power, but we cannot take much comfort in the fact when a dictatorial fascist government rules all the same. Perhaps in the far future, we may have our moment in the sun once more. We will have a duty to do better then than we have now.

- @game_over: Game over.

@fight_harzburg
on-arrival: spd_rebel = 1
go-to: civil_war

@gessler_crisis
on-arrival: chancellor = "Groener"; spd_toleration = 0; chancellor_party = "I"; time_to_election = 12+strife; strife -= 3; sa_militancy -= 0.1; rb_militancy -= 0.1 if rb_militancy >= 0.1; rfb_militancy -= 0.1; sh_militancy -= 0.05; reichswehr_loyalty += 0.1;
go-to: fascist_uprising if coup_progress >= 10 and sa_force+sh_force >= 50 and nsdap_r+dnvp_r >= 40; next_election_time if coup_progress < 10 or sa_force+sh_force < 45 or nsdap_r+dnvp_r < 35

Otto Gessler has become increasingly influenced by his inner cadre of military men and seeing the total inability of the Reichstag to form a government has caved to their increasing calls for martial law. 

He has asked Wilhelm Groener to lead a temporary military government and, wielding the powers of Article 48 of the German constitution, declared martial law and cancelled the next elections. 

@adenauer_crisis
on-arrival: chancellor = "Stegerwald"; adenauer_emergency = 1; spd_toleration = 0; chancellor_party = "Z"; time_to_election = 3; sa_militancy -= 0.05; sh_militancy -= 0.05; rfb_militancy -= 0.05; reichswehr_loyalty += 0.05; old_middle_z += 5; new_middle_z += 5; rural_z += 5; catholics_z += 8; workers_z += 3; unemployed_z += 2; old_middle_nsdap -= 4; new_middle_nsdap -= 4; rural_nsdap -= 4; workers_nsdap -= 4; unemployed_nsdap -= 3; pro_republic += 10
go-to: next_election_time

Adenauer, tired of the chaos, sacks the current chancellor and replaces him with Adam Stegerwald, who has launched a sweeping campaign for the Center Party, calling for law and order and harshly attacking the Nazis as being, in a sense, 'posers', for throwing Germany into disarray while claiming to desire nothing but its greatness.

@eckener_crisis
on-arrival: chancellor = "Groener"; spd_toleration = 0; chancellor_party = "I"; time_to_election = 6+strife; strife -= 3; sa_militancy -= 0.1; rb_militancy -= 0.1 if rb_militancy >= 0.1; rfb_militancy -= 0.1; sh_militancy -= 0.05; reichswehr_loyalty += 0.05;
go-to: fascist_uprising if coup_progress >= 10 and sa_force+sh_force >= 45 and nsdap_r+dnvp_r >= 35; next_election_time if coup_progress < 10 or sa_force+sh_force < 45 or nsdap_r+dnvp_r < 35

Hugo Eckener, seeing the devolving situation in Germany and convinced that a strong hand is needed, appoints Wilhelm Groener chancellor of a temporary military cabinet and declared martial law, swearing this as a temporary measure to secure Germany's future.

@fascist_uprising
on-arrival: sh_strength *= 0.8; sh_strength *= 0.9 if president == "Eckener"; reichswehr_loyalty += 0.1; reichswehr_loyalty += 0.1 if president == "Gessler"; nazi_rebel = 1

Infuriated by this action, the Nazi SA launches an armed insurrection, attempting to seize and take over key infrastructure across the country as the army is deployed against them. Due to the circumstances of the uprising, the Harzburg Front is split, and many in the Stalhelm hesitate.

- @civil_war: Nonetheless, it is a civil war.

# todo: SPD response section if no fascist uprising, eg general strike against martial law..?

@nothing_to_do
on-arrival: reichswehr_loyalty -= 0.1; sh_strength += 100; sa_strength += 100; pro_republic -= 5; coup_progress += no_majority_elections/2

With presidential powers weakened, there is nothing that can be done. The republic may not last much longer.

@next_election_time
on-arrival: next_election_time = time+time_to_election; next_election_month = month + time_to_election; next_election_year = year; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; 