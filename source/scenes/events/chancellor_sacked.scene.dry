title: Hindenburg Sacks [+ chancellor +]!
subtitle: Hindenburg has turned on us...
view-if: (president == "Hindenburg" and presidential_powers != 1 and spd_in_government == 1 and ((reichswehr_loyalty <= 0.05 or ((capital_strike_seen or in_left_front) and unemployed >= 20 and budget < 1 and economic_democracy+nationalization_progress >= 3 and land_reform > 0)) or (hindenburg_angry >= 100)) and ((coup_progress >= 3 and chancellor_party == "SPD") or (coup_progress >= 6 and chancellor_party == "Z"))) and papen_chancellor_timer <= 0 and chancellor != "Brüning"
new-page: true
on-arrival: dvp_relation -= 8; z_relation -= 4 if chancellor_party != "Z"; pro_republic -= 3; pro_republic -= 1 if spd_r >= 30; pro_republic -= 1 if weimar_coalition >= 50; coup_progress -= 1; spd_sackings += 1; judicial_reform -= 1
go-to: bruning_right if right_coalition >= 50 and not right_coalition_failed and year < 1929; no_majority if right_coalition < 50 or right_coalition_failed or year >= 1929
tags: event

= [+ chancellor +] Sacked

It seems that in our attempts to pursue our policy plans and dismantle the power that the reactionaries hold over our republic, we have earned the ire of President Hindenburg, who has today unilaterally dismissed our government as is unfortunately legal by the German constitution. Some on the left of our party are calling to resist but this does not seem to be a coup, not yet anyway.

[? if (in_weimar_coalition and black_thursday_seen and (spd_votes+ddp_votes+(z_votes-3)) < (30+coup_progress+capital_strike_progress)) : Hindenburg claims this is due to the increasing unpopularity of our cabinet and inability to deal with the crisis effectively. ?]

@bruning_right
on-arrival: spd_in_government = 0; spd_toleration = 0; chancellor = "Brüning"; chancellor_party = "Z"; foreign_minister_party = "DVP"; interior_minister_party = "Z"; justice_minister_party = "Z"; labor_minister_party = "Z"; reichswehr_minister_party = "I"; economic_minister_party = "Z"; finance_minister_party = "Z"; agriculture_minister_party = "Z"; in_right_coalition = 1

Replacing us is Brüning of the Center Party, who has reorganized his cabinet to encompass the right-wing parties, including the DNVP who reluctantly accede to Hindenburg's wishes for a government. Their coalition frustratingly holds a majority in the Reichstag, meaning we are forced to accept the loss and prepare for the next elections.

@no_majority
on-arrival: spd_in_government = 0; spd_toleration = 1; chancellor = "Brüning"; chancellor_party = "Z"; foreign_minister_party = "DVP"; interior_minister_party = "Z"; justice_minister_party = "Z"; labor_minister_party = "Z"; reichswehr_minister_party = "I"; economic_minister_party = "Z"; finance_minister_party = "Z"; agriculture_minister_party = "Z"

Replacing us is Brüning of the Center Party, who has reorganized his cabinet to encompass the bourgeois parties. Although they lack a majority in the Reichstag, we could tolerate their government at the next session to prevent new elections, effectively acquiscing to Hindenburg's wishes.

- @tolerate: We will enter a toleration agreement.
- @dont_tolerate: No majority, no government. Elections it is!

@tolerate
on-arrival: left_dissent += 5

We will tolerate Brüning's government, protecting it from motions of no confidence in the name of stability.

@dont_tolerate
on-arrival: time_to_election = 3; spd_in_government = 0; in_grand_coalition = 0; spd_toleration = 0; weimar_dissent += 1 if weimar_plan_adopted
call: set_next_election_time

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].

# todo: call a general strike with the ADGB not as an insurrection but to intimidate Hindenburg into acquiscing 